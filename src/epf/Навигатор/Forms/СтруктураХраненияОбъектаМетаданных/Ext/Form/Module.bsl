#Область Форма

&НаСервере
Перем Навигатор;

#Область События

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнформацияОСтруктуре = "ВТерминах1С";
	
	ЗаполнитьЗначенияПеременных();
	
	УстановитьЗаголовок();
	
	ПолучитьДанныеСтруктурыХранения();
	
	// Получить размер данных
	ВключитьОбъекты = Новый Массив;
	ВключитьОбъекты.Добавить(СтрШаблон("%1.%2", ИмяЭлементаКоллекции, ИмяОбъектаБазы));
	
	РазмерДанных = ПолучитьРазмерДанныхБазыДанных(Неопределено, ВключитьОбъекты, Неопределено);
	
	Элементы.НадписьРазмерДанных.Заголовок = СтрШаблон("Размер данных: %1 байт", Формат(РазмерДанных, "ЧН=0; ЧГ=3,0"));
	
КонецПроцедуры

#КонецОбласти

#Область Элементы

&НаКлиенте
Процедура ИнформацияОСтруктуреПриИзменении(Элемент)
	
	ПолучитьДанныеСтруктурыХранения();
	
КонецПроцедуры

#КонецОбласти

#Область Таблицы

#Область ТаблицыХранения

&НаКлиенте
Процедура ТаблицыХраненияПриАктивизацииСтроки(Элемент)
	
	#Область Предусловия
	
	ТекущиеДанные = Элементы.ТаблицыХранения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	#КонецОбласти
	
	Попытка
		ДополнительныеДанныеСтруктурыХранения = ПолучитьИзВременногоХранилища(АдресДополнительныеДанные);
		ДополнительныеДанные = ДополнительныеДанныеСтруктурыХранения.Получить(ТекущиеДанные.ИмяТаблицыХранения);
		
		Элементы.СтраницаПоля.Заголовок = "Поля";
		Если ДополнительныеДанные.КоличествоПолей > 0 Тогда
			Элементы.СтраницаПоля.Заголовок = СтрШаблон("Поля (%1)",    ДополнительныеДанные.КоличествоПолей);
		КонецЕсли;
		
		Элементы.СтраницаИндексы.Заголовок = "Индексы";
		Если ДополнительныеДанные.КоличествоИндексов > 0 Тогда
			Элементы.СтраницаИндексы.Заголовок = СтрШаблон("Индексы (%1)", ДополнительныеДанные.КоличествоИндексов);
		КонецЕсли;
	Исключение
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Команды
#КонецОбласти

#КонецОбласти

#Область Методы

&НаСервере
Процедура ЗаполнитьЗначенияПеременных()
	
	ИмяКоллекции         = Параметры.ИмяКоллекции;
	ИмяОбъектаБазы       = Параметры.ИмяОбъектаБазы;
	ИмяЭлементаКоллекции = Параметры.ИмяЭлементаКоллекции;
	
	ПолноеИмяОбъекта = СтрШаблон(
		"%1.%2", 
		ИмяЭлементаКоллекции, 
		ИмяОбъектаБазы
	);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовок()
	
	Заголовок = СтрШаблон("%1.%2", ИмяЭлементаКоллекции, ИмяОбъектаБазы);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеСтруктурыХранения()
	
	Навигатор = ЭтаОбработка();
	
	ИменаОбъектовМетаданных = Новый Массив;
	ИменаОбъектовМетаданных.Добавить(СтрШаблон("%1.%2", ИмяЭлементаКоллекции, ИмяОбъектаБазы));
	
	ИменаБазыДанных = ?(ИнформацияОСтруктуре = "ВТерминахСУБД", Истина, Ложь);
	
	ТаблицаОписанияСтруктуры = ПолучитьСтруктуруХраненияБазыДанных(ИменаОбъектовМетаданных, ИменаБазыДанных);
	
	ТаблицыХранения.Очистить();
	
	УдалитьДобавитьДополнительныеРеквизиты(Истина, Ложь);
	ЗначениеВРеквизитФормы(ТаблицаОписанияСтруктуры, "ТаблицыХранения");
	
	ДополнитьСтруктуруХранения();
	
	ПолучитьДополнительныеДанныеСтруктурыХранения();
	
	// Установить свойства элементов на форме
	Элементы.СтраницаТаблицыХранения.Заголовок = СтрШаблон("Таблицы (%1)", ТаблицыХранения.Количество());
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДобавитьДополнительныеРеквизиты(Удалить = Истина, Добавить = Истина)
	
	НовыеРеквизиты = Новый Массив;
	НовыеРеквизиты.Добавить(Новый РеквизитФормы("ПредставлениеТаблицы", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(250, ДопустимаяДлина.Переменная)), "ТаблицыХранения", "Имя таблицы"));
	
	Если Удалить Тогда
		Попытка
			УдаляемыеРеквизиты = Новый Массив;
			УдаляемыеРеквизиты.Добавить("ТаблицыХранения.ПредставлениеТаблицы");
			
			ИзменитьРеквизиты(, УдаляемыеРеквизиты);
		Исключение
			//
		КонецПопытки;
	КонецЕсли;
	
	Если Добавить Тогда
		ИзменитьРеквизиты(НовыеРеквизиты);
	КонецЕсли;
	
	НовыеРеквизиты.Очистить();
	НовыеРеквизиты.Добавить(Новый РеквизитФормы("ПредставлениеТипаЗначенияПоля", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная)), "ТаблицыХранения.Поля", "Тип"));
	
	Если Удалить Тогда
		Попытка
			УдаляемыеРеквизиты = Новый Массив;
			УдаляемыеРеквизиты.Добавить("ТаблицыХранения.Поля.ПредставлениеТипаЗначенияПоля");
			
			ИзменитьРеквизиты(, УдаляемыеРеквизиты);
		Исключение
			//
		КонецПопытки;
	КонецЕсли;
	
	Если Добавить Тогда
		ИзменитьРеквизиты(НовыеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДобавитьДополнительныеЭлементыУправления(Удалить = Истина, Добавить = Истина)
	
	Если Удалить Тогда
		Попытка
			Элементы.Удалить(Элементы.ПредставлениеТаблицы);
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Если Добавить Тогда
		Элемент = Элементы.Добавить("ПредставлениеТаблицы", Тип("ПолеФормы"), Элементы.ТаблицыХранения);
		Элемент.Вид = ВидПоляФормы.ПолеВвода;
		Элемент.ПутьКДанным = "ТаблицыХранения.ПредставлениеТаблицы";
		Элемент.Ширина = 15;
		Элемент.ЦветТекста = Новый Цвет(51, 51, 153);
		Элемент.ТолькоПросмотр = Истина;
		
		Элементы.Переместить(Элемент, Элементы.ТаблицыХранения, Элементы.ТаблицыХраненияИмяТаблицы);
	КонецЕсли;
	
	Если Удалить Тогда
		Попытка
			Элементы.Удалить(Элементы.ПредставлениеТипаЗначенияПоля);
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Если Добавить Тогда
		Элемент = Элементы.Добавить("ПредставлениеТипаЗначенияПоля", Тип("ПолеФормы"), Элементы.ТаблицыХраненияПоля);
		Элемент.Вид = ВидПоляФормы.ПолеВвода;
		Элемент.ПутьКДанным = "ТаблицыХранения.Поля.ПредставлениеТипаЗначенияПоля";
		Элемент.Ширина = 15;
		Элемент.ЦветТекста = WebЦвета.ПолночноСиний;
		Элемент.Шрифт = Новый Шрифт(,,,Истина);
		Элемент.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьСтруктуруХранения()
	
	УдалитьДобавитьДополнительныеРеквизиты();
	УдалитьДобавитьДополнительныеЭлементыУправления();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДополнительныеДанныеСтруктурыХранения()
	
	ОбъектМетаданных = Метаданные[ИмяКоллекции][ИмяОбъектаБазы];
	
	// Добавить данные по количеству полей и индексов
	АдресДополнительныеДанные = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	ДополнительныеДанныеСтруктурыХранения = Новый Соответствие;
	
	СоответствиеПолей = Новый Соответствие;
	
	Для каждого стр_ТаблицыХранения Из ТаблицыХранения Цикл
		
		ДополнительныеДанныеСтруктурыХранения.Вставить(
			стр_ТаблицыХранения.ИмяТаблицыХранения, 
				Новый Структура(
					"КоличествоПолей,
					|КоличествоИндексов", 
					стр_ТаблицыХранения.Поля.Количество(),
					стр_ТаблицыХранения.Индексы.Количество()
				)
		);
		
		Если стр_ТаблицыХранения.Назначение = "Основная" Или стр_ТаблицыХранения.Назначение = "ИтогиСрезПоследних" Или стр_ТаблицыХранения.Назначение = "НастройкиХраненияИтоговРегистраСведений" Тогда
			
			Для каждого Реквизит Из ОбъектМетаданных.СтандартныеРеквизиты Цикл
				
				НайденныеСтроки = стр_ТаблицыХранения.Поля.НайтиСтроки(Новый Структура("ИмяПоля", Реквизит.Имя));
				
				Если Не (НайденныеСтроки.Количество() = 0) Тогда
					
					стр_Поля = НайденныеСтроки[0];
					стр_Поля.ПредставлениеТипаЗначенияПоля = Навигатор.СтрокаОписанияТипов(Реквизит.Тип);
					
					Если ЗначениеЗаполнено(стр_Поля.ИмяПоля) Тогда
						СоответствиеПолей.Вставить(стр_Поля.ИмяПоляХранения, стр_Поля);
					КонецЕсли;
					
				Иначе
					
					Для каждого эл_СоответствиеПолей Из СоответствиеПолей Цикл
						
						НайденныеСтроки = стр_ТаблицыХранения.Поля.НайтиСтроки(Новый Структура("ИмяПоляХранения", эл_СоответствиеПолей.Значение.ИмяПоляХранения));
						
						Если Не (НайденныеСтроки.Количество() = 0) Тогда
							стр_Поля = НайденныеСтроки[0];
							стр_Поля.ПредставлениеТипаЗначенияПоля = эл_СоответствиеПолей.Значение.ПредставлениеТипаЗначенияПоля;
							стр_Поля.ИмяПоля = эл_СоответствиеПолей.Значение.ИмяПоля;
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Для каждого Реквизит Из ОбъектМетаданных.Реквизиты Цикл
				
				НайденныеСтроки = стр_ТаблицыХранения.Поля.НайтиСтроки(Новый Структура("ИмяПоля", Реквизит.Имя));
				Если Не (НайденныеСтроки.Количество() = 0) Тогда
					
					стр_Поля = НайденныеСтроки[0];
					стр_Поля.ПредставлениеТипаЗначенияПоля = Навигатор.СтрокаОписанияТипов(Реквизит.Тип);
					
					Если ЗначениеЗаполнено(стр_Поля.ИмяПоля) Тогда
						СоответствиеПолей.Вставить(стр_Поля.ИмяПоляХранения, стр_Поля);
					КонецЕсли;
					
				Иначе
					
					Для каждого эл_СоответствиеПолей Из СоответствиеПолей Цикл
						
						НайденныеСтроки = стр_ТаблицыХранения.Поля.НайтиСтроки(Новый Структура("ИмяПоляХранения", эл_СоответствиеПолей.Значение.ИмяПоляХранения));
						
						Если Не (НайденныеСтроки.Количество() = 0) Тогда
							стр_Поля = НайденныеСтроки[0];
							стр_Поля.ПредставлениеТипаЗначенияПоля = эл_СоответствиеПолей.Значение.ПредставлениеТипаЗначенияПоля;
							стр_Поля.ИмяПоля = эл_СоответствиеПолей.Значение.ИмяПоля;
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если Метаданные.РегистрыСведений.Содержит(ОбъектМетаданных) Или Метаданные.РегистрыБухгалтерии.Содержит(ОбъектМетаданных) 
				Или Метаданные.РегистрыНакопления.Содержит(ОбъектМетаданных) Или Метаданные.РегистрыРасчета.Содержит(ОбъектМетаданных) Тогда
				
				Для каждого Реквизит Из ОбъектМетаданных.Измерения Цикл
					
					НайденныеСтроки = стр_ТаблицыХранения.Поля.НайтиСтроки(Новый Структура("ИмяПоля", Реквизит.Имя));
					Если Не (НайденныеСтроки.Количество() = 0) Тогда
						
						стр_Поля = НайденныеСтроки[0];
						стр_Поля.ПредставлениеТипаЗначенияПоля = Навигатор.СтрокаОписанияТипов(Реквизит.Тип);
						
						Если ЗначениеЗаполнено(стр_Поля.ИмяПоля) Тогда
							СоответствиеПолей.Вставить(стр_Поля.ИмяПоляХранения, стр_Поля);
						КонецЕсли;
						
					Иначе
						
						Для каждого эл_СоответствиеПолей Из СоответствиеПолей Цикл
							
							НайденныеСтроки = стр_ТаблицыХранения.Поля.НайтиСтроки(Новый Структура("ИмяПоляХранения", эл_СоответствиеПолей.Значение.ИмяПоляХранения));
							
							Если Не (НайденныеСтроки.Количество() = 0) Тогда
								стр_Поля = НайденныеСтроки[0];
								стр_Поля.ПредставлениеТипаЗначенияПоля = эл_СоответствиеПолей.Значение.ПредставлениеТипаЗначенияПоля;
								стр_Поля.ИмяПоля = эл_СоответствиеПолей.Значение.ИмяПоля;
							КонецЕсли;
							
						КонецЦикла;
						
					КонецЕсли;
					
				КонецЦикла;
				
				Для каждого Реквизит Из ОбъектМетаданных.Ресурсы Цикл
					
					НайденныеСтроки = стр_ТаблицыХранения.Поля.НайтиСтроки(Новый Структура("ИмяПоля", Реквизит.Имя));
					Если Не (НайденныеСтроки.Количество() = 0) Тогда
						
						стр_Поля = НайденныеСтроки[0];
						стр_Поля.ПредставлениеТипаЗначенияПоля = Навигатор.СтрокаОписанияТипов(Реквизит.Тип);
						
						Если ЗначениеЗаполнено(стр_Поля.ИмяПоля) Тогда
							СоответствиеПолей.Вставить(стр_Поля.ИмяПоляХранения, стр_Поля);
						КонецЕсли;
						
					Иначе
						
						Для каждого эл_СоответствиеПолей Из СоответствиеПолей Цикл
							
							НайденныеСтроки = стр_ТаблицыХранения.Поля.НайтиСтроки(Новый Структура("ИмяПоляХранения", эл_СоответствиеПолей.Значение.ИмяПоляХранения));
							
							Если Не (НайденныеСтроки.Количество() = 0) Тогда
								стр_Поля = НайденныеСтроки[0];
								стр_Поля.ПредставлениеТипаЗначенияПоля = эл_СоответствиеПолей.Значение.ПредставлениеТипаЗначенияПоля;
								стр_Поля.ИмяПоля = эл_СоответствиеПолей.Значение.ИмяПоля;
							КонецЕсли;
							
						КонецЦикла;
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
			УстановитьПредставлениеТипаЗначенияПоля(стр_ТаблицыХранения, "NumberPrefix", "ПрефиксНомера", "Дата");
			УстановитьПредставлениеТипаЗначенияПоля(стр_ТаблицыХранения, "_NumberPrefix", "ПрефиксНомера", "Дата");
			
		ИначеЕсли стр_ТаблицыХранения.Назначение = "ТабличнаяЧасть" Тогда
			
			ИмяТабличнойЧасти = СтрРазделить(стр_ТаблицыХранения.ИмяТаблицы, ".")[2];
			
			Если ИмяТабличнойЧасти = "ВидыСубконто" Тогда
				ТабличнаяЧасть = ОбъектМетаданных[ИмяТабличнойЧасти];
			Иначе
				ТабличнаяЧасть = ОбъектМетаданных.ТабличныеЧасти[ИмяТабличнойЧасти];
			КонецЕсли;
			
			Для каждого Реквизит Из ТабличнаяЧасть.Реквизиты Цикл
				
				НайденныеСтроки = стр_ТаблицыХранения.Поля.НайтиСтроки(Новый Структура("ИмяПоля", Реквизит.Имя));
				Если Не (НайденныеСтроки.Количество() = 0) Тогда
					стр_Поля = НайденныеСтроки[0];
					стр_Поля.ПредставлениеТипаЗначенияПоля = Навигатор.СтрокаОписанияТипов(Реквизит.Тип);
				КонецЕсли;
				
			КонецЦикла;
			
			НайденныеСтроки = стр_ТаблицыХранения.Поля.НайтиСтроки(Новый Структура("ИмяПоля", "НомерСтроки"));
			Если Не (НайденныеСтроки.Количество() = 0) Тогда
				стр_Поля = НайденныеСтроки[0];
				стр_Поля.ПредставлениеТипаЗначенияПоля = Навигатор.СтрокаОписанияТипов(Новый ОписаниеТипов("Число"));
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ДополнительныеДанныеСтруктурыХранения, АдресДополнительныеДанные);
	
	// Добавить данные представления имен
	Для каждого стр_ТаблицыХранения Из ТаблицыХранения Цикл
		
		стр_ТаблицыХранения.ПредставлениеТаблицы = СтрЗаменить(стр_ТаблицыХранения.ИмяТаблицы, ПолноеИмяОбъекта, "");
		стр_ТаблицыХранения.ПредставлениеТаблицы = СтрЗаменить(стр_ТаблицыХранения.ПредставлениеТаблицы, ".", "");
		
		Если Не ЗначениеЗаполнено(стр_ТаблицыХранения.ПредставлениеТаблицы) Тогда
			стр_ТаблицыХранения.ПредставлениеТаблицы = ПолноеИмяОбъекта;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеТипаЗначенияПоля(стр_ТаблицыХранения, ИмяПоляХранения, ИмяПоля, ИмяТипа)
	
	НайденныеСтроки = стр_ТаблицыХранения.Поля.НайтиСтроки(Новый Структура("ИмяПоляХранения", ИмяПоляХранения));
	Если Не (НайденныеСтроки.Количество() = 0) Тогда
		стр_Поля = НайденныеСтроки[0];
		стр_Поля.ИмяПоля = ИмяПоля;
		стр_Поля.ПредставлениеТипаЗначенияПоля = Навигатор.СтрокаОписанияТипов(Новый ОписаниеТипов(ИмяТипа));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЭтаОбработка()
	
	Если Навигатор = Неопределено Тогда
		Результат = РеквизитФормыВЗначение("Объект");
	Иначе
		Результат = Навигатор;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Типы

&НаСервере
Функция Тип_ОписаниеТипов()
	
	Результат = Новый ОписаниеТипов;
	Результат = Новый ОписаниеТипов(Результат, Справочники.ТипВсеСсылки().Типы());
	Результат = Новый ОписаниеТипов(Результат, Документы.ТипВсеСсылки().Типы());
	Результат = Новый ОписаниеТипов(Результат, Перечисления.ТипВсеСсылки().Типы());
	Результат = Новый ОписаниеТипов(Результат, ПланыСчетов.ТипВсеСсылки().Типы());
	Результат = Новый ОписаниеТипов(Результат, ПланыВидовХарактеристик.ТипВсеСсылки().Типы());
	Результат = Новый ОписаниеТипов(Результат, Задачи.ТипВсеСсылки().Типы());
	Результат = Новый ОписаниеТипов(Результат, ПланыВидовРасчета.ТипВсеСсылки().Типы());
	Результат = Новый ОписаниеТипов(Результат, ПланыОбмена.ТипВсеСсылки().Типы());
	Результат = Новый ОписаниеТипов(Результат, "Строка");
	Результат = Новый ОписаниеТипов(Результат, "Число");
	Результат = Новый ОписаниеТипов(Результат, "Дата");
	Результат = Новый ОписаниеТипов(Результат, "Булево");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти