#Область Форма

#Область События
#КонецОбласти

#Область Элементы
#КонецОбласти

#Область Таблицы

#Область ИмяТаблицы
#КонецОбласти

#КонецОбласти

#Область Команды
#КонецОбласти

#КонецОбласти

#Область Методы

&НаКлиенте
Процедура Настроить(Форма, ИмяЭлементаКомандыНастройки) Экспорт
	
	#Область Предусловия
	
	Если Не ЗначениеЗаполнено(ИмяЭлементаКомандыНастройки) Тогда
		Возврат;
	КонецЕсли;
	
	#КонецОбласти
	
	ЧастиСтроки = СтрРазделить(ИмяЭлементаКомандыНастройки, Объект.Система_РазделительЧастейИмен, Ложь);
	
	ИмяКоллекции         = ЧастиСтроки[3];
	ИмяОбъектаМетаданных = ЧастиСтроки[5];
	
	// Установить заголовок.
	Форма.Элементы.НастройкиКолонокСпискаИмя.Заголовок = ЧастиСтроки[4] + "." + ЧастиСтроки[5];
	
	// Определить имя элемента списка.
	ЧастиСтрокиИмениЭлементаСписка = Новый Массив;
	ЧастиСтрокиИмениЭлементаСписка.Добавить(ЧастиСтроки[2]);
	ЧастиСтрокиИмениЭлементаСписка.Добавить(ЧастиСтроки[3]);
	ЧастиСтрокиИмениЭлементаСписка.Добавить(ЧастиСтроки[4]);
	ЧастиСтрокиИмениЭлементаСписка.Добавить(ЧастиСтроки[5]);
	ЧастиСтрокиИмениЭлементаСписка.Добавить(ЧастиСтроки[6]);
	
	ИмяЭлементаСписка = СтрСоединить(ЧастиСтрокиИмениЭлементаСписка, Объект.Система_РазделительЧастейИмен);
	
	// Получить таблицу элементов списка.
	ТаблицаКолонок.Очистить();
	
	ПараметрыКолонокСписка = ПараметрыКолонок(ИмяКоллекции, ИмяОбъектаМетаданных);
	
	Для каждого КлючЗначение Из ПараметрыКолонокСписка Цикл
		
		Для каждого РеквизитСписка Из КлючЗначение.Значение Цикл
			стр_ТаблицаКолонок = ТаблицаКолонок.Добавить();
			стр_ТаблицаКолонок.ИмяКолонки = РеквизитСписка.ИмяРеквизита;
			стр_ТаблицаКолонок.Видимость  = РеквизитСписка.Видимость;
		КонецЦикла;
		
	КонецЦикла;
	
	// Получить параметры колонок списка.
	
	Дерево = Форма["НастройкиКолонокСписка"];
	КоллекцияЭлементовДерева = Дерево.ПолучитьЭлементы();
	
	Для каждого КлючЗначение Из ПараметрыКолонокСписка Цикл
		
		Если КлючЗначение.Значение.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		стр_ГруппаРеквизитов = КоллекцияЭлементовДерева.Добавить();
		стр_ГруппаРеквизитов.ТипЗаписи = "ГруппаКолонок";
		стр_ГруппаРеквизитов.Имя = КлючЗначение.Ключ;
		
		Если КлючЗначение.Ключ = "СтандартныеРеквизиты" Тогда
			стр_ГруппаРеквизитов.НомерКартинки = 1;
		ИначеЕсли КлючЗначение.Ключ = "Реквизиты" Тогда
			стр_ГруппаРеквизитов.НомерКартинки = 2;
		ИначеЕсли КлючЗначение.Ключ = "Измерения" Тогда
			стр_ГруппаРеквизитов.НомерКартинки = 3;
		ИначеЕсли КлючЗначение.Ключ = "Ресурсы" Тогда
			стр_ГруппаРеквизитов.НомерКартинки = 4;
		КонецЕсли;
		
		стр_ГруппаРеквизитов.ИмяЭлементаСписка = ИмяЭлементаСписка;
		
		КоллекцияЭлементовДереваРеквизиты = стр_ГруппаРеквизитов.ПолучитьЭлементы();
		
		Для каждого ПараметрыКолонки Из КлючЗначение.Значение Цикл
			
			стр_Реквизит = КоллекцияЭлементовДереваРеквизиты.Добавить();
			стр_Реквизит.ТипЗаписи = "Колонка";
			стр_Реквизит.Имя = ПараметрыКолонки.ИмяРеквизита;
			стр_Реквизит.НомерКартинки = стр_ГруппаРеквизитов.НомерКартинки;
			стр_Реквизит.ИмяЭлементаСписка = ИмяЭлементаСписка;
			стр_Реквизит.ПредставлениеТипа = ПараметрыКолонки.ПредставлениеТипа;
			стр_Реквизит.Уровень = 1;
			стр_Реквизит.Синоним = ПараметрыКолонки.Синоним;
			
			СтрокиТаблицыКолонок = ТаблицаКолонок.НайтиСтроки(Новый Структура("ИмяРеквизита", ПараметрыКолонки.ИмяРеквизита));
			
			Если Не (СтрокиТаблицыКолонок.Количество() = 0) Тогда
				стр_Реквизит.ИмяКолонки = СтрокиТаблицыКолонок[0].ИмяКолонки;
				стр_Реквизит.Видимость  = СтрокиТаблицыКолонок[0].Видимость;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Форма.Элементы.НастройкиКолонокСпискаВидимость.Заголовок = Форма.Элементы[ИмяЭлементаСписка].Родитель.Заголовок;
	Форма.Элементы.ГруппаНастройкаКолонокСписка.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Развернуть(Форма) Экспорт
	
	Дерево = Форма["НастройкиКолонокСписка"];
	ЭлементУправления = Форма.Элементы["НастройкиКолонокСписка"];
	
	Для каждого Узел Из Дерево.ПолучитьЭлементы() Цикл
		
		ИдентификаторУзла = Узел.ПолучитьИдентификатор();
		
		Попытка
			ЭлементУправления.Развернуть(ИдентификаторУзла, Истина);
		Исключение
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#Область Навигатор

&НаСервере
Функция ПараметрыКолонок(ИмяКоллекции, ИмяОбъектаМетаданных)
	
	Навигатор = Навигатор();
	Результат = Навигатор.ПараметрыКолонокСпискаЭлементовОбъектаМетаданных(ИмяКоллекции, ИмяОбъектаМетаданных);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбщегоНазначения

&НаСервере
Функция Навигатор()
	
	Результат = РеквизитФормыВЗначение("Объект");
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Подключаемые
#КонецОбласти
