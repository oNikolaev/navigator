#Область Форма

#Область События

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Навигатор = ЭтаОбработка();
	
	ОбъектБазы = Параметры.ОбъектБазы;
	
	Если Не ЗначениеЗаполнено(ОбъектБазы) Тогда
		ОбъектБазы = Перечисления[ОбъектБазы.Метаданные().Имя].Получить(0);
	КонецЕсли;
	
	Заголовок = "(...) " + ОбъектБазы;
	
	ПеречислениеМетаданные = ОбъектБазы.Метаданные();
	ИмяВидаПеречисления = ПеречислениеМетаданные.Имя;
	
	ЗаполнитьВсеЗначенияПеречисления(ОбъектБазы);
	ЗаполнитьВсеРеквизиты();
	
	Элементы.НадписьОбъектКонфигурацииИнфо.Заголовок = Навигатор.ПредставлениеТипаОбъектаКонфигурации(ОбъектБазы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = "Навигатор" Тогда
		
		Если ИмяСобытия = "ЗакрытьВсеОкнаРедакторов" Тогда
			
			Попытка
				Закрыть();
			Исключение
				//
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Элементы
#КонецОбласти

#Область Таблицы

#Область ЗначенияПеречисления

&НаКлиенте
Процедура ЗначенияПеречисленияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	#Область Предусловия
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	#КонецОбласти
	
	Если Поле.Имя = "ЗначенияПеречисленияИмя" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПеречислениеСсылка = ЗначениеПеречисленияПоИмени(ИмяВидаПеречисления, ТекущиеДанные.Имя);
		ИмяФормыРедактора = ИмяФормыРедактораЗначения(ПеречислениеСсылка);
		
		Если Не ЗначениеЗаполнено(ИмяФормыРедактора) Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура("ОбъектБазы", ПеречислениеСсылка);
		
		ПолноеИмяФормы = ПолноеИмяФормыОбработки(ЭтаФорма.ИмяФормы, ИмяФормыРедактора);
		ОткрытьФорму(ПолноеИмяФормы, ПараметрыФормы, ЭтаФорма, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Команды
#КонецОбласти

#КонецОбласти

#Область Методы

&НаСервере
Процедура ЗаполнитьВсеЗначенияПеречисления(ПеречислениеСсылка)
	
	ПеречислениеМетаданные = ПеречислениеСсылка.Метаданные();
	ИмяВида = ПеречислениеМетаданные.Имя;
	
	тз_ЗначенияПеречисления = РеквизитФормыВЗначение("ЗначенияПеречисления");
	тз_ЗначенияПеречисления.Очистить();
	
	Для Каждого ЗначениеПеречисления Из ПеречислениеМетаданные.ЗначенияПеречисления Цикл
		стр = тз_ЗначенияПеречисления.Добавить();
		ЗаполнитьЗначенияСвойств(стр, ЗначениеПеречисления);
		
		стр.Порядок = ПорядокЗначенияПеречисления(Перечисления[ИмяВида][стр.Имя]);
		
		Если ПеречислениеСсылка = Перечисления[ИмяВида][стр.Имя] Тогда
			стр.ТекущееЗначение = Истина;
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(тз_ЗначенияПеречисления, "ЗначенияПеречисления");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВсеРеквизиты()
	
	#Область Предусловия
	
	Если Не ЗначениеЗаполнено(ОбъектБазы) Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеОбъекта = ОбъектБазы.Метаданные();
	
	#КонецОбласти
	
	ЭтаОбработка = ЭтаОбработка();
	
	дз_ВсеРеквизиты = РеквизитФормыВЗначение("ВсеРеквизиты");
	дз_ВсеРеквизиты.Строки.Очистить();
	
	//
	
	ПорядокСортировки = 0;
	
	стр_СистемныеРеквизиты = дз_ВсеРеквизиты.Строки.Добавить();
	стр_СистемныеРеквизиты.Имя               = "СистемныеРеквизиты";
	стр_СистемныеРеквизиты.Представление     = ЭтаОбработка.ПодставитьПараметрыВСтроку("СистемныеРеквизиты (%1)", 2);
	стр_СистемныеРеквизиты.ПорядокСортировки = ПорядокСортировки;
	стр_СистемныеРеквизиты.Системный         = Истина;
	стр_СистемныеРеквизиты.ТипСтроки         = "СистемныеРеквизиты";
	стр_СистемныеРеквизиты.НомерКартинки     = 51;
	стр_СистемныеРеквизиты.ТолькоПросмотр    = Истина;
	
	//
	
	НавигационнаяСсылка = ЭтаОбработка.НавигационнаяСсылкаНаЭлемент(ОбъектБазы);
	
	Если ЗначениеЗаполнено(НавигационнаяСсылка) Тогда
		
		ПорядокСортировки = ПорядокСортировки + 1;
		
		стр_СистемныйРеквизит = стр_СистемныеРеквизиты.Строки.Добавить();
		стр_СистемныйРеквизит.Имя               = "НавигационнаяСсылка";
		стр_СистемныйРеквизит.Представление     = "НавигационнаяСсылка";
		стр_СистемныйРеквизит.ПорядокСортировки = ПорядокСортировки;
		стр_СистемныйРеквизит.Системный         = Истина;
		стр_СистемныйРеквизит.ТипСтроки         = "СистемныйРеквизит";
		стр_СистемныйРеквизит.Тип               = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(0), , );
		стр_СистемныйРеквизит.ПредставлениеТипа = ЭтаОбработка.СтрокаОписанияТипов(стр_СистемныйРеквизит.Тип);
		стр_СистемныйРеквизит.Синоним           = "Навигационная ссылка";
		стр_СистемныйРеквизит.Значение          = НавигационнаяСсылка;
		стр_СистемныйРеквизит.НомерКартинки     = 51;
		стр_СистемныеРеквизиты.ТолькоПросмотр   = Истина;
		
	КонецЕсли;
	
	//
	
	ПорядокСортировки = ПорядокСортировки + 1;
	
	стр_СистемныйРеквизит = стр_СистемныеРеквизиты.Строки.Добавить();
	стр_СистемныйРеквизит.Имя               = "Представление";
	стр_СистемныйРеквизит.Представление     = "Представление";
	стр_СистемныйРеквизит.ПорядокСортировки = ПорядокСортировки;
	стр_СистемныйРеквизит.Системный         = Истина;
	стр_СистемныйРеквизит.ТипСтроки         = "СистемныйРеквизит";
	стр_СистемныйРеквизит.Тип               = Новый ОписаниеТипов("Строка");
	стр_СистемныйРеквизит.ПредставлениеТипа = ЭтаОбработка.СтрокаОписанияТипов(стр_СистемныйРеквизит.Тип);
	стр_СистемныйРеквизит.Синоним           = "Представление";
	стр_СистемныйРеквизит.Значение          = Строка(ОбъектБазы);
	стр_СистемныйРеквизит.НомерКартинки     = 51;
	стр_СистемныеРеквизиты.ТолькоПросмотр   = Истина;
	
	стр_СистемныеРеквизиты.Представление = ЭтаОбработка.ПодставитьПараметрыВСтроку("СистемныеРеквизиты (%1)", стр_СистемныеРеквизиты.Строки.Количество());
	
	//
	
	Если МетаданныеОбъекта.СтандартныеРеквизиты.Количество() > 0 Тогда
		
		ПорядокСортировки = ПорядокСортировки + 1;
		
		стр_СтРеквизиты = дз_ВсеРеквизиты.Строки.Добавить();
		стр_СтРеквизиты.Имя               = "СтандартныеРеквизиты";
		стр_СтРеквизиты.Представление     = ЭтаОбработка.ПодставитьПараметрыВСтроку("СтандартныеРеквизиты (%1)", МетаданныеОбъекта.СтандартныеРеквизиты.Количество());
		стр_СтРеквизиты.ПорядокСортировки = ПорядокСортировки;
		стр_СтРеквизиты.Стандартный       = Истина;
		стр_СтРеквизиты.НомерКартинки     = 44;
		стр_СтРеквизиты.ТипСтроки         = "СтандартныеРеквизиты";
		
		Для каждого СтандартныйРеквизит Из МетаданныеОбъекта.СтандартныеРеквизиты Цикл
			
			ПорядокСортировки = ПорядокСортировки + 1;
			
			стр_СтРеквизит = стр_СтРеквизиты.Строки.Добавить();
			стр_СтРеквизит.Имя               = СтандартныйРеквизит.Имя;
			стр_СтРеквизит.Представление     = СтандартныйРеквизит.Имя;
			стр_СтРеквизит.Тип               = СтандартныйРеквизит.Тип;
			стр_СтРеквизит.ПредставлениеТипа = ЭтаОбработка.СтрокаОписанияТипов(СтандартныйРеквизит.Тип);
			стр_СтРеквизит.Синоним           = СтандартныйРеквизит.Синоним;
			Если СтандартныйРеквизит.Имя = "Ссылка" Тогда
				стр_СтРеквизит.Значение = ОбъектБазы;
			ИначеЕсли СтандартныйРеквизит.Имя = "Порядок" Тогда
				стр_СтРеквизит.Значение = ПорядокЗначенияПеречисления(ОбъектБазы);
			Иначе
				стр_СтРеквизит.Значение = ОбъектБазы[СтандартныйРеквизит.Имя];
			КонецЕсли;
			стр_СтРеквизит.ПорядокСортировки = ПорядокСортировки;
			стр_СтРеквизит.Стандартный       = Истина;
			стр_СтРеквизит.НомерКартинки     = 44;
			стр_СтРеквизит.ТипСтроки         = "СтандартныйРеквизит";
			
			Если СтандартныйРеквизит.Имя = "Ссылка" Тогда
				стр_СтРеквизит.ТолькоПросмотр = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		стр_СтРеквизиты.Строки.Сортировать("Имя");
		
	КонецЕсли;
	
	//
	
	дз_ВсеРеквизиты.Строки.Сортировать("ПорядокСортировки, Имя");
	
	ЗначениеВРеквизитФормы(дз_ВсеРеквизиты, "ВсеРеквизиты");
	
КонецПроцедуры

&НаСервере
Функция ПорядокЗначенияПеречисления(ПеречислениеСсылка)
	
	ПеречислениеМетаданные = ПеречислениеСсылка.Метаданные();
	ИмяВида = ПеречислениеМетаданные.Имя;
	
	Порядок = 0;
	
	Для Каждого ЗначениеПеречисления Из ПеречислениеМетаданные.ЗначенияПеречисления Цикл
		
		Порядок = Порядок + 1;
		
		Если ПеречислениеСсылка = Перечисления[ИмяВида][ЗначениеПеречисления.Имя] Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Порядок;
	
КонецФункции

&НаСервере
Функция ЗначениеПеречисленияПоИмени(ИмяВида, ИмяЗначения)
	
	Возврат Перечисления[ИмяВида][ИмяЗначения];
	
КонецФункции

&НаСервере
Функция ЭтаОбработка()
	
	Результат = РеквизитФормыВЗначение("Объект");
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолноеИмяФормыОбработки(ИмяТекущейФормы, ИмяФормыОбработки)
	
	Результат = ЭтаОбработка().ПолноеИмяФормыОбработки(ИмяТекущейФормы, ИмяФормыОбработки);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ИмяФормыРедактораЗначения(ЗначениеДляРедактирования)
	
	Навигатор = РеквизитФормыВЗначение("Объект");
	Результат = Навигатор.ИмяФормыРедактораЗначения(ЗначениеДляРедактирования);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Подключаемые
#КонецОбласти

#Область Типы
#КонецОбласти

#Область Тесты
#КонецОбласти